Public Function WaitFor(ByRef driver As Object, _
                        ByVal selectorType As String, _
                        Optional ByVal selectorValue As String = "", _
                        Optional ByVal timeoutSec As Double = 5) As Object
    Dim startTime As Single
    Dim result As Object
    
    startTime = Timer
    
    Do
        On Error Resume Next
        Select Case LCase(selectorType)
            Case "name"
                Set result = driver.FindElementByName(selectorValue)
            Case "id"
                Set result = driver.FindElementById(selectorValue)
            Case "css"
                Set result = driver.FindElementByCss(selectorValue)
            Case "xpath"
                Set result = driver.FindElementByXPath(selectorValue)
            Case "linktext"
                Set result = driver.FindElementByLinkText(selectorValue)
            Case "partiallink"
                Set result = driver.FindElementByPartialLinkText(selectorValue)
            Case "alert"
                Set result = driver.SwitchToAlert
            Case Else
                Err.Raise vbObjectError + 1, , "Unsupported selectorType: " & selectorType
        End Select
        On Error GoTo 0
        
        If Not result Is Nothing Then
            Set WaitFor = result
            Exit Function
        End If
        
        DoEvents
        If Timer - startTime > timeoutSec Then Exit Do
    Loop
    
    Set WaitFor = Nothing
End Function
